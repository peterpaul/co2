/*
 * libco2-base Library with basic datatypes in CO2/carbon
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/RefObject
include co2/DoubleLinkedList
include co2/Iterator

class DoubleLinkedListIterator : BaseObject, Iterator {
	DoubleLinkedItem item;
	
	DoubleLinkedListIterator (DoubleLinkedList list) {
		super ();
		item = list.head;
	}

	~DoubleLinkedListIterator () {
		item = null;
	}

	void findNext () {
		while (item && item.item == null) {
			item = item.next;
		}
	}

	int hasNext () {
		findNext ();
		return item != null;
	}

	RefObject getNext () {
		if (hasNext ()) {
			DoubleLinkedItem link = item;
			item = item.next;
			return link.item;
		}
		return null;
	}
}
