/*
 * libco2-base Library with basic datatypes in CO2/carbon
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/Set
include co2/RefObject
include co2/Array
include co2/HashSetIterator

class HashSet : RefObject, Set {
	Array entries;
	unsigned length;

	HashSet (unsigned size) {
		super ();
		entries = new Array (size);
		entries.retain ();
		length = 0;
	}

	~HashSet () {
		entries.release ();
	}

	unsigned nextIndex (unsigned index) {
		return (index + 1) % entries.size;

	}

	unsigned getIndexFor (RefObject item) {
		unsigned index = item.hashCode () % entries.size;
		RefObject entry;
		while ((entry = entries.get (index))) {
			if (entry.equals (item)) {
				return index;
			}
			index = nextIndex (index);
		}
		return index;
	}

	int contains (RefObject item) {
		unsigned index = getIndexFor (item);
		RefObject entry = (RefObject) entries.get (index);
		return entry != null;
	}

	RefObject add (RefObject item) {
		unsigned index = getIndexFor (item);
		if (entries.get (index)) {
			length -= 1;
		}
		entries.set (index, item);
		length += 1;
		return item;
	}

	RefObject remove (RefObject item) {
		unsigned index = getIndexFor (item);
		RefObject entry = (RefObject) entries.get (index);
		if (entry) {
			length -= 1;
			RefObject result = entry.retain ();
			entries.set (index, null);
			return result.autoRelease ();
		} else {
			return null;
		}		
	}

	unsigned size () {
		return length;		
	}

	Iterator iterator () {
		return new HashSetIterator (self);
	}
}
