/*
 * libco2-base Library with basic datatypes in CO2/carbon
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/RefObject

class DoubleLinkedItem : RefObject {
	DoubleLinkedItem prev, next;
	RefObject item;

	DoubleLinkedItem (RefObject item) {
		super ();
		self.item = item?.retain ();
		prev = next = null;
	}

	DoubleLinkedItem.linked (RefObject item, DoubleLinkedItem prev, DoubleLinkedItem next) {
		super ();
		self.item = item?.retain ();
		link (prev, next);
	}

	~DoubleLinkedItem () {
		if (prev) {
			prev.next = next;
		}
		if (next) {
			next.prev = prev;
		}
		item?.release ();
	}

	RefObject set (RefObject item) {
		item?.retain ();
		self.item?.release ();
		self.item = item;
		return item;
	}

	void link (DoubleLinkedItem left, DoubleLinkedItem right) {
		prev = left;
		if (left) {
			left.next = self;
		}
		next = right;
		if (right) {
			right.prev = self;
		}
	}
}
