/*
 * libco2-base Library with basic datatypes in CO2/carbon
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/BaseObject
include co2/ReleasePool

class RefObject : BaseObject {
	int refCount;

	RefObject() {
		refCount = 0;
		// add self to auto release pool
		current_release_pool.add(self);
	}

	~RefObject() {
		if (refCount <= 0) {
			// remove self from auto release pool
			current_release_pool.remove(self);
		}
	}

	RefObject retain() {
		if (refCount <= 0) {
			// remove self from auto release pool
			current_release_pool.remove(self);
			refCount = 0;
		}
		refCount += 1;
		return self;
	}

	RefObject release() {
		refCount -= 1;
		if (refCount == 0) {
			delete self;
			self = null;
		}
		return self;
	}

	RefObject autoRelease() {
		refCount -= 1;
		if (refCount == 0) {
			current_release_pool.add(self);
		}
		return self;
	}
}
