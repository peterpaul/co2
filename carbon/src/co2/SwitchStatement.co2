/*
 * carbon Compiler and programming language for libco2
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/Statement

class SwitchStatement : Statement {
	Expression expr;
	RefList cases;

	SwitchStatement (Expression expr, RefList cases) {
		super ();
		self.expr = (Expression) expr.retain ();
		self.cases = (RefList) cases.retain ();
	}

	~SwitchStatement () {
		expr.release ();
		cases.release ();
	}

	void accept (CompileObjectVisitor visitor) {
		expr.accept (visitor);
		cases.map_args (compileobject_accept, visitor);
		visitor.visit (self);
	}

	void type_check () {
		current_context?.add (self);
		expr.type_check ();
		cases.map (compileobject_type_check);
		current_context?.remove_last ();
	}

	void generate (FILE *fp) {
		fprintf (fp, "switch (");
		expr.generate (fp);
		fprintf (fp, ") {\n");
		cases.map_args (compileobject_generate, fp);
		fprintf (fp, "}\n");
	}
}
