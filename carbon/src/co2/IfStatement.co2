/*
 * carbon Compiler and programming language for libco2
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/Statement
include co2/Expression

class IfStatement : Statement {
	Expression condition;
	Statement then_clause;
	Statement else_clause;

	IfStatement (Expression condition, Statement then_clause, Statement else_clause) {
		super ();
		self.condition = (Expression) condition.retain ();
		self.then_clause = (Statement) then_clause.retain ();
		self.else_clause = (Statement) else_clause?.retain ();
	}

	~IfStatement () {
		condition.release ();
		then_clause.release ();
		else_clause?.release ();
	}

	void accept (CompileObjectVisitor visitor) {
		condition.accept (visitor);
		then_clause.accept (visitor);
		else_clause?.accept (visitor);
		visitor.visit (self);
	}

	void type_check () {
		condition.type_check ();
		then_clause.type_check ();
		else_clause?.type_check ();
	}

	void generate (FILE *fp) {
		fprintf (fp, "if (");
		condition.generate (fp);
		fprintf (fp, ")\n");
		then_clause.generate (fp);
		if (else_clause) {
			fprintf (fp, "else\n");
			else_clause.generate (fp);
		}
	}
}
