/*
 * carbon Compiler and programming language for libco2
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/Declaration
include co2/Token
include co2/Scope
include co2/RefList

class ObjectTypeDeclaration : Declaration {
	RefList members;
	Scope member_scope;

	ObjectTypeDeclaration (Token name) {
		super (name);
	}

	~ObjectTypeDeclaration () {
		members?.release ();
		member_scope?.release ();
	}

	Bool is_compatible (ObjectTypeDeclaration other);
}

void objecttypedeclaration_generate_method_arguments(ArgumentDeclaration arg, va_list *app) {
	FILE * fp = va_arg (*app, FILE *);
	fprintf (fp, ", ");
	arg.generate (fp);
}

void objecttypedeclaration_generate_method_argument_names(ArgumentDeclaration arg, va_list *app) {
	FILE * fp = va_arg (*app, FILE *);
	fprintf (fp, ", ");
	arg.name.generate (fp);
}

void objecttypedeclaration_generate_method_registration_2 (FunctionDeclaration method_decl, va_list *app) {
	FILE * fp = va_arg (*app, FILE *);
	ObjectTypeDeclaration class_decl = (ObjectTypeDeclaration) va_arg (*app, void *);
	if (method_decl.body) {
		fprintf (fp, "O_OBJECT_METHOD (");
		class_decl.name.generate (fp);
		fprintf (fp, ", ");
		method_decl.name.generate (fp);
		fprintf (fp, ");\n");
	} else {
		if (method_decl.implemented_methods && method_decl.implemented_methods.length > 0) {
			InterfaceMethodDefinition imd = (InterfaceMethodDefinition) method_decl.implemented_methods.get (0);
			fprintf (fp, "O_OBJECT_METHOD (");
			imd.interface_name.generate (fp);
			fprintf (fp, ", ");
			method_decl.name.generate (fp);
			fprintf (fp, ");\n");
		}
	}
}
