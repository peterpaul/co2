/*
 * carbon Compiler and programming language for libco2
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/BaseCompileObjectVisitor
include co2/Context
include co2/HashSet
include co2/CompilerResources

class GenerateSourceIncludesVisitor : BaseCompileObjectVisitor {
	Set set;

	GenerateSourceIncludesVisitor (FILE *fp) {
		super (fp);
		current_context = new Context ();
		current_context.retain ();
		set = new HashSet (127);
	}

	~GenerateSourceIncludesVisitor () {
		current_context.release ();
		current_context = null;
		delete set;
	}

	void addFile (File file) {
		if (file && file != main_file) {
			String filename = new String ("\"%s.h\"", file.name.data);
			set.add (filename);
		}
	}

	void visitDeclaration (Declaration decl) {
		if (decl.include_file) {
			set.add (decl.include_file.name);
		}
		if (decl) {
			addFile (decl.file);
		}
	}

	void visitTokenExpression (TokenExpression expr) {
		if (expr.decl) {
			addFile (expr.decl.file);
		}

		if (expr.type && expr.type is_of ObjectType) {
			visitObjectType ((ObjectType) expr.type);
		}
	}

	void visitObjectType (ObjectType type) {
		if (type.decl) {
			addFile (type.decl.file);
		}
	}

	void visitFile (File file) {
		Iterator it = set.iterator ();
		while (it.hasNext ()) {
			String value = (String) it.getNext ();
			fprintf (fp, "#include %s\n", value.data);
		}
	}
}
