/*
 * carbon Compiler and programming language for libco2
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/Type
include co2/Token

class PrimitiveType : Type {
	Token token;

	PrimitiveType (Token token) {
		super ();
		self.token = (Token) token.retain ();
	}

	~PrimitiveType () {
		token.release ();
	}

	void generate (FILE *fp) {
		token.generate (fp);
	}

	Token get_token () {
		return token;
	}

	String toString () {
		return new String ("%s", token.name.data);
	}

	Bool is_compatible (Type _other) {
		if (super (_other)) {
			if (_other is_of ObjectType) {
				ObjectType other = (ObjectType) _other;
				return other.is_compatible (self);
			}
			PrimitiveType other = (PrimitiveType) _other;
			Token name_self = get_token ();
			Token name_other = other.get_token ();
			if ((name_self.type == _INT || name_self.type == _UNSIGNED) &&
				(name_other.type == _INT || name_other.type == _UNSIGNED) ||
				(name_self.type == _FLOAT || name_self.type == _DOUBLE) &&
				(name_other.type == _FLOAT || name_other.type == _DOUBLE)) {
				if (name_self.type != name_other.type) {
					warning (name_self, "possible data loss while converting %s to %s\n", name_other.name.data, name_self.name.data);
				}
				return true;
			} else {
				return name_self.type == name_other.type;
			}
		}
		return false;
	}

	void accept (CompileObjectVisitor visitor) {
		token.accept (visitor);
		visitor.visit (self);
	}
}
