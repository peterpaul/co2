/*
 * carbon Compiler and programming language for libco2
 *
 * Copyright (C) 2011,2012 Peterpaul Taekele Klein Haneveld
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
include co2/Statement
include co2/RefList
include co2/CatchStatement

class TryStatement : Statement {
	Statement try_clause;
	RefList catch_clause;
	Statement finally_clause;

	TryStatement (Statement try_clause, RefList catch_clause, Statement finally_clause) {
		super ();
		self.try_clause = (Statement) try_clause.retain ();
		self.catch_clause = (RefList) catch_clause.retain ();
		self.finally_clause = (Statement) finally_clause?.retain ();
	}

	~TryStatement () {
		try_clause.release ();
		catch_clause.release ();
		finally_clause?.release ();
	}

	void accept (CompileObjectVisitor visitor) {
		try_clause.accept (visitor);
		catch_clause.map_args (compileobject_accept, visitor);
		finally_clause?.accept (visitor);
		visitor.visit (self);
	}

	void type_check () {
		current_context?.add (self);
		try_clause.type_check ();
		catch_clause.map (compileobject_type_check);
		finally_clause?.type_check ();
		current_context?.remove_last ();
	}

	void generate (FILE *fp) {
		fprintf (fp, "try {\n");
		try_clause.generate (fp);
		fprintf (fp, "ex_pop ();\n");
		if (finally_clause) {
			fprintf (fp, "do_finally;\n");
		}
		fprintf (fp, "}\n");

		catch_clause.map_args (trystatement_generate_catch, fp, self);

		if (finally_clause) {
			fprintf (fp, "finally {\n");
			finally_clause.generate (fp);
			fprintf (fp, "throw (1, NULL, NULL, 0, NULL);\n");
			fprintf (fp, "}\n");
			fprintf (fp, "end_try_finally;\n");
		} else {
			fprintf (fp, "end_try;\n");
		}
	}
}

void trystatement_generate_catch (CatchStatement catch_statement, va_list *app) {
	FILE *fp = va_arg (*app, FILE *);
	TryStatement try_statement = (TryStatement) va_arg (*app, void *);
	ObjectType type = (ObjectType) catch_statement.argument.type;
	fprintf (fp, "catch (%s ()) {\n", type.token.name.data);
	type.generate (fp);
	catch_statement.argument.name.generate (fp);
	fprintf (fp, " = (");
	type.generate (fp);
	fprintf (fp, ") ex_stack->ex_data;\n");
	catch_statement.generate (fp);
	fprintf (fp, "ex_pop ();\n");
	if (try_statement.finally_clause) {
		fprintf (fp, "do_finally;\n");
	}
	fprintf (fp, "}\n");
}
