include co2/HashMap
include co2/RefObject
include co2/IScope
include co2/Error

class Scope : RefObject, IScope {
	ScopeType type;
	Token name;
	Scope parent;
	HashMap map;

	Scope (ScopeType type, Token name) {
		super ();
		self.type = type;
		self.name = name;
		parent = current_scope;
		map = new HashMap (127);
		current_scope = self;
	}

	~Scope () {
		name?.release ();
	}

	IScope get_parent () {
		return parent;
	}

	IScope set_parent (IScope parent_scope) {
		return parent = parent_scope;
	}

	Declaration declare (Declaration decl) {
		map.put (decl.name, decl);
		decl.set_scope (self);
	}

	void leave () {
		current_scope = parent;
	}

	Declaration lookup_in_this_scope (Token token) {
		map.get (token);
	}

	Declaration lookup (Token token) {
		Declaration result = lookup_in_this_scope (token);
		if (!result) {
			if (!parent) {
				error_not_found (token);
			} else {
				result = parent.lookup (token);
			}
		}
		return result;
	}

	Declaration find_type_in_this_scope (Token token, Class type) {
		Declaration result = (Declaration) map.get (token);
		if (result != null && !(result is_of type)) {
			!result;
		}
		return result;
	}

	Declaration find_type (Token token, Class type) {
		Declaration result = find_type_in_this_scope (token, type);
		if (!result && parent != null) {
			result = parent.find_type (token, type);
		}
		return result;
	}
 
	Bool exists_in_this_scope (Token token) {
		return map.get (token) != null;
	}

	Bool exists (Token token) {
		Bool result = exists_in_this_scope (token);
		if (result == false && parent != null) {
			result = parent.exists (token);
		}
		return result;
	}

	void error_not_found (Token token) {
		error (token, "'%s' not declared.\n", token.name.data);
	}
}
